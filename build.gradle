// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript { scriptHandler ->
    apply from: 'repositories.gradle',
            to: scriptHandler
    ext.versions = [
            'compileSdk'      : 30,
            'minSdk'          : 21,
            'targetSdk'       : 30,
            'appCompat'       : '1.2.0',
            'constraintLayout': '2.0.4',
            'coreKtx'         : '1.3.2',
            'coreTesting'     : '2.1.0',
            'coroutines'      : '1.3.9',
            'dagger'          : '2.23.2',
            'kotlin'          : '1.4.10',
            'material'        : '1.2.1',
            'junit'           : '4.13.1',
            'extJunit'        : '1.1.2',
            'espresso'        : '3.3.0',
            'room'            : '2.2.5',
            'ktlint'          : '0.36.0',
    ]
    ext.names = [
            'applicationId': 'com.budgetizer'
    ]
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.8.1'
}

subprojects {
    buildscript {
        apply from: rootProject.file('repositories.gradle')
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('scripts/Copyright.kt')
        }
        groovyGradle {
            target '**/*.gradle'
            licenseHeaderFile project.rootProject.file('scripts/Copyright.kt'), '(buildscript|apply)'
        }
    }

    if (project.hasProperty("kapt")) {
        kapt {
            useBuildCache = true
        }
    }
}
